/*
 * This build file was generated by the Gradle 'init' task.
 *
 * This generated file contains a sample Groovy project to get you started.
 * For more details take a look at the Groovy Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/4.4/userguide/tutorial_groovy_projects.html
 */
buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.jacobono:gradle-jaxb-plugin:1.3.6'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}


// Apply the groovy plugin to add support for Groovy
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.jacobono.jaxb'

idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }


group = 'lx.econta'
version = "1.3.1"
description = """
    Groovy library to generate  all the XML files required by what is known as Mexican Electronic Accouting 
    (Contabilidad electronica) according to the federal disponsition Anexo 24  SAT updated to version 1.3
"""

// Compatibilidad minima para Java 1.8
sourceCompatibility = 1.8
targetCompatibility = 1.8

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    javadoc.failOnError = false
    from javadoc.destinationDir
}


// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    mavenCentral()
    mavenLocal()
}

jaxb {
    System.setProperty('javax.xml.accessExternalSchema', 'all')
    xsdDir = "src/main/resources/schemas"
    xjc {
        generatePackage = "econta.demo"
    }
}

dependencies {
    // Use the latest Groovy version for building this library
    compile 'org.codehaus.groovy:groovy-all:2.4.12'

    // Use the awesome Spock testing and specification framework
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    // Dependencies for com.github.jacobono.jaxb Gradle plugin
    jaxb 'com.sun.xml.bind:jaxb-xjc:2.2.7-b41'
    jaxb 'com.sun.xml.bind:jaxb-impl:2.2.7-b41'
    jaxb 'javax.xml.bind:jaxb-api:2.2.7'
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publications = ['maven']
    publish = true
    override = true
    pkg {
        repo = 'econta'
        name = "${project.name}"
        desc = "${project.description}"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lxcancino/econta-lib'
        labels = ['Contabilidad elctronica','Anexo 24']
        version {
            name = "${project.version}"
            desc = "${project.description}"
            released = new java.util.Date().format('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ')
        }
    }
}

task bintrayCredentials {
    doLast {
        println "Bintray user: ${bintrayUser}"
        println "Bintray API key:  ${bintrayKey}"
        println "${project.name}"
    }
}

task getHomeDir {
    doLast {
        println gradle.gradleHomeDir
    }
}

